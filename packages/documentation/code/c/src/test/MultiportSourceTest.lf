 // Check bank of reactors sending to bank of reactors.
target C {
    timeout: 2 sec,
    fast: true,
};
import MultiportSource from "../MultiportSource.lf";
reactor Destination(
    bank_index:int(0)
) {
    state s:int(0);
    input in:int;
    reaction(in) {=
        info_print("Destination %d received: %d.", self->bank_index, in->value);
        if (in->value != self->s) {
            error_print_and_exit("Expected %d.", self->s);
        }
        self->s += self->bank_index;
    =}
    reaction(shutdown) {=
        if (self->s == 0 && self->bank_index != 0) {
            error_print_and_exit("Destination %d received no input!", self->bank_index);
        }
        info_print("Success.");
    =}
}

main reactor(width:int(4)) { 
    a = new[width] MultiportSource();
    b = new[width] Destination();
    a.out -> b.in;
}
